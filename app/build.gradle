apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'koin'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 31
    ndkVersion "21.0.6113669"
    defaultConfig {
        applicationId "io.zenandroid.onlinego"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 52
        versionName "2021.11.29.${versionCode}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        buildConfigField "String", "CLIENT_ID", '"' + System.getenv("ONLINE_GO_CLIENT_ID") + '"'
        buildConfigField "String", "CLIENT_SECRET", '"' + System.getenv("ONLINE_GO_CLIENT_SECRET") + '"'
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    kotlinOptions {
        jvmTarget = "1.8"
        useIR = true
    }
    buildFeatures {
        compose true
        viewBinding true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
            version "3.10.2"
        }
    }
    lintOptions {
        // if set to true (default), stops the build if errors are found.
        abortOnError false   
    }
    signingConfigs {
        debug {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.bqv'
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        resources.excludes.add("META-INF/*")
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'

    implementation "io.coil-kt:coil-compose:1.3.2"
    //
    // Warning! do not upgrade this beyond 4.4.x as 4.5.0 brings in websocket compression
    // which for whatever reason causes issues.
    //
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.2'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:4.9.2'
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'

    implementation 'com.squareup.moshi:moshi-kotlin:1.11.0'
    implementation 'com.squareup.moshi:moshi-adapters:1.11.0'

    implementation "androidx.navigation:navigation-fragment-ktx:2.4.0-alpha10"
    implementation "androidx.navigation:navigation-ui-ktx:2.4.0-alpha10"
    implementation "androidx.core:core-ktx:1.7.0-beta02"

    // rxandroid
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'

    implementation "androidx.work:work-runtime-ktx:2.7.0-rc01"
    implementation "androidx.work:work-rxjava2:2.7.0-rc01"

    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"

    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'androidx.annotation:annotation:1.0.0'

    // groupie
    implementation 'com.xwray:groupie:2.8.1'
    implementation 'com.xwray:groupie-viewbinding:2.8.1'

    implementation 'com.google.android.material:material:1.5.0-alpha04'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.emoji:emoji-appcompat:1.1.0'
    implementation 'androidx.emoji:emoji:1.1.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.ui:ui-util:$compose_version"

    implementation 'me.zhanghai.android.materialprogressbar:library:1.6.1'

    implementation 'com.github.sephiroth74:NumberSlidingPicker:v1.0.3'

    implementation 'com.github.blennerSilva:AwesomeDialog:2.0.6'

    implementation('io.socket:socket.io-client:2.0.1') {
        // excluding org.json which is provided by Android
        exclude group: 'org.json', module: 'json'
    }

    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"

    implementation 'com.jakewharton.rxbinding2:rxbinding:2.2.0'

    implementation 'br.com.simplepass:loading-button-android:2.1.5'

    implementation 'io.noties.markwon:core:4.3.1'

    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'

    implementation "io.insert-koin:koin-android:3.1.0"

    implementation 'androidx.room:room-runtime:2.3.0'
    implementation 'androidx.room:room-rxjava2:2.3.0'

    implementation 'com.jakewharton.threetenabp:threetenabp:1.3.1'
    implementation 'com.github.cpiz:BubbleView:1.0.3'

    implementation "com.google.accompanist:accompanist-pager:0.18.0"
    implementation "com.google.accompanist:accompanist-pager-indicators:0.18.0"

    kapt 'androidx.room:room-compiler:2.3.0'

    implementation 'androidx.test.espresso:espresso-idling-resource:3.4.0'

    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    implementation 'com.github.tony19:logback-android:2.0.0'
    implementation('com.github.toomasr:sgf4j:sgf4j-parser-0.0.6') {
        // Force to use android logging
        exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-api'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-core'
    }

    implementation 'com.vmadalin:easypermissions-ktx:1.0.0'

    testImplementation 'com.nhaarman:mockito-kotlin-kt1.1:1.6.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation "io.insert-koin:koin-test-junit4:3.1.0"

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
}
repositories {
    mavenCentral()
}
